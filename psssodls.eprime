language ESSENCE' 1.0
given     n : int
letting   RANGE be domain int(0..n-1)
letting   THESUM be (n*(n-1)/2)

find      field: matrix indexed by [RANGE, RANGE] of RANGE

such that
  $ all rows have to be different
  forAll row : RANGE .
       allDiff(field[row,..]),

  $ all columns have to be different
  forAll col : RANGE .
       allDiff(field[..,col]),

  $ pandiagonality constraints
  forAll w : RANGE .
      (sum i: RANGE . 
          field[i, (i + w) % n]) = THESUM,
  forAll w : RANGE .
      (sum i: RANGE . 
          field[i, (w - i) % n]) = THESUM,

  $ strongly-symmetric constraints
  forAll i,j : RANGE .
      field[i, j] + field[n - 1 - i, n - 1 - j] = n - 1,

  $ orthogonality constraints
  forAll i,j,k,l : RANGE .
      i<k \/ j<l ->  field[i,j]!=field[k,l] \/ field[j,i]!=field[l,k]

